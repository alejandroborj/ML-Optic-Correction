doplot(iii): macro = {
  write,table=twiss,file="output/twissiii.dat";
  plot, haxis=s, vaxis1={betx,bety}, vaxis2={mux,muy}, interpolate, colour=100;
  plot, haxis=s, vaxis1={betx,bety}, vaxis2={dx,dpx} , interpolate, colour=100;
}

! TWISS1
use, period=lhcb1, range = S.CELL.78.B1/E.CELL.78.B1;
savebeta, label = bini1ir8, place = S.CELL.78.B1;
twiss, sequence=lhcb1;
show, bini1ir8;
exec, doplot(1);

bini1ir8->mux = 0;
bini1ir8->muy = 0;

! TWISS2
use, period=lhcb1, range = S.CELL.81.B1/E.CELL.81.B1;
savebeta, label = bini2ir8, place = E.CELL.81.B1;
twiss, sequence=lhcb1;
show, bini2ir8;
exec, doplot(2);

! TWISS3
use, period=lhcb1, range = S.CELL.78.B1/S.DS.L8.B1;
savebeta, label = bir8b1, place = S.DS.L8.B1;
twiss, sequence=lhcb1, beta0 = bini1ir8;
show, bir8b1;
exec, doplot(3);

bir8b1->mux = 0;
bir8b1->muy = 0;

! TWISS4
seqedit, sequence=lhcb1;
reflect;
endedit;

bini1ir8->betx =  bini2ir8->betx;
bini1ir8->bety =  bini2ir8->bety;
bini1ir8->alfx = -bini2ir8->alfx;
bini1ir8->alfy = -bini2ir8->alfy;
bini1ir8->dx   =  bini2ir8->dx;
bini1ir8->dpx  = -bini2ir8->dpx;
bini1ir8->mux  =  0;
bini1ir8->muy  =  0;
show, bini1ir8;

use, period=lhcb1, range = E.CELL.81.B1/E.DS.R8.B1;
savebeta, label = eir8b1, place = E.DS.R8.B1;
twiss, sequence=lhcb1, beta0=bini1ir8;
show, eir8b1;
exec, doplot(4);

eir8b1->mux  = muxip8;
eir8b1->muy  = muyip8;
eir8b1->alfx = -eir8b1->alfx;
eir8b1->alfy = -eir8b1->alfy;
eir8b1->dpx  = -eir8b1->dpx;
eir8b1->dpy  = -eir8b1->dpy;
show, eir8b1;

seqedit, sequence=lhcb1;
reflect;
endedit;


! TWISS5
use, period=lhcb1, range = S.DS.L8.B1/E.DS.R8.B1;
savebeta, label = bip8b1, place = IP8;
twiss, sequence=lhcb1, beta0=bir8b1;
show, bip8b1;
exec, doplot(5);

Use, period=lhcb1, range = S.DS.L8.B1/E.DS.R8.B1;

return;
